plugins {
	id 'com.github.kt3k.coveralls' version '2.4.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'

group                        = 'net.querz.nbt'
archivesBaseName             = 'nbt'
version                      = '4.1'
sourceCompatibility          = '1.8'
targetCompatibility          = '1.8'
compileJava.options.encoding = 'UTF-8'

repositories {
	jcenter()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compile
	destinationDir = file("./doc/")
	options.windowTitle 'NBT'
	options.encoding 'UTF-8'
	options.linkSource true
	options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

jar {
	manifest {
		attributes('Automatic-Module-Name': 'net.querz.nbt')
	}
}
